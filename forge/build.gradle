plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}
architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "the_bumblezone-common.mixins.json"
        mixinConfig "the_bumblezone-forge.mixins.json"

        runs {
            all {
                property 'mixin.env.remapRefMap', 'true'
                property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            }
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
    shadowMinecraftLibrary
    minecraftLibraries.extendsFrom(shadowMinecraftLibrary)
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    modCompileOnly("curse.maven:pokecube_aio-285121:${project.pokecubeaio_version}")

    modCompileOnly("curse.maven:productive_bees-377897:${project.productivebees_file_id}")
    //modRuntimeOnly("curse.maven:productive_bees-377897:${project.productivebees_file_id}")

    modCompileOnly("curse.maven:pokecube_aio-285121:${project.pokecubeaio_version}")

    modCompileOnly("curse.maven:buzzier_bees-355458:${project.buzzier_bees_file_id}")

    modCompileOnly("curse.maven:quark-243121:${project.quark_file_id}")
    modCompileOnly("curse.maven:autoreglib-250363:${project.autoreglib_file_id}")

    modCompileOnly("mezz.jei:jei-${project.minecraft_version}-forge:${project.jei}")

    modLocalRuntime("com.telepathicgrunt:CommandStructures-Forge:${project.commandstructures_forge}")
    modLocalRuntime("com.telepathicgrunt:StructureVoidToggle-Forge:${project.structurevoidtoggle_forge}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    include "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
    implementation "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
    forgeRuntimeLibrary "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
    annotationProcessor "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon, project.configurations.shadowMinecraftLibrary]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

def ENV = System.getenv()

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact remapJar
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifactId = "$rootProject.name"

            pom {
                name = 'TheBumblezone'
                description = 'A wonderful bee dimension mod with lots of new exploration and adventuring to do!'
                url = 'https://github.com/TelepathicGrunt/Bumblezone'

                scm {
                    connection = 'git:https://github.com/TelepathicGrunt/Bumblezone.git'
                    developerConnection = 'git:https://github.com/TelepathicGrunt/Bumblezone.git'
                    url = 'https://github.com/TelepathicGrunt/Bumblezone'
                }

                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://github.com/TelepathicGrunt/Bumblezone/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'telepathic_grunt'
                        name = 'TelepathicGrunt'
                        email = 'asarnesespam@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://maven.resourcefulbees.com/repository/telepathicgrunt/"
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASS
            }
        }
    }
}

task buildAndRelease{}
tasks.publish.mustRunAfter tasks.build
tasks.curseforge.mustRunAfter tasks.publish
tasks.modrinth.mustRunAfter tasks.publish
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.curseforge, tasks.modrinth)