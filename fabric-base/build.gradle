plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    common("fabric", "quilt", "forge")
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}") {
        force = true
    }

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${rootProject.cardinal_components}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${rootProject.cardinal_components}"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${rootProject.cardinal_components}"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${rootProject.cardinal_components}"

    modImplementation("dev.cafeteria:fake-player-api:${rootProject.fake_player_api}") {
        transitive = false //This API doesnt use fabric but includes it so we need to exclude it as its from 1.19.1
    }
    include "dev.cafeteria:fake-player-api:${rootProject.fake_player_api}"

    modImplementation "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rootProject.rei}"
    modImplementation "me.shedaniel:RoughlyEnoughItems-api-fabric:${rootProject.rei}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }

    include "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
    modImplementation "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
    annotationProcessor "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}